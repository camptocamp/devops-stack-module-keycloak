= OIDC Bootstrap

A https://devops-stack.io[DevOps Stack] module to bootstrap a realm, an administrators group with one or more users and an OIDC client in order to use https://www.keycloak.org/[Keycloak] as an OIDC provider.

This module allows you to have a working authentication provider for the DevOps Stack without having to configure Keycloak manually.

IMPORTANT: Because the main use of this module is to have a working Keycloak instance in a development environment, it provides a sensible configuration with some secure enough defaults. However, *it is not recommended to be used in a production environment*. In that case, we recommend you use this module as an example. Simply take a look at the code and the https://registry.terraform.io/providers/mrparkers/keycloak/latest/docs[provider documentation] to get an idea on how to use it to manage you Keycloak instance.

== Usage

After deploying Keycloak using the main module on this repository, first you need to add the provider configuration necessary on your root module:

[source,terraform]
----
terraform {
  required_providers {
    keycloak = {
      source = "mrparkers/keycloak"
    }
  }
}

provider "keycloak" {
  client_id                = "admin-cli"
  username                 = module.keycloak.admin_credentials.username
  password                 = module.keycloak.admin_credentials.password
  url                      = "https://keycloak.apps.${local.cluster_name}.${format("%s.nip.io", replace(module.ingress.external_ip, ".", "-"))}"
  initial_login            = false # Do no try to setup the provider before Keycloak is provisioned.
  tls_insecure_skip_verify = true # Since we are in a testing environment, do not verify the authenticity of SSL certificates.
}
----

NOTE: The argument `initial_login` absolutely needs to be set as `false`, otherwise Terraform will try to connect to Keycloak before it is deployed. The argument `tls_insecure_skip_verify` needs to be set as `false` only on testing environments when using self-signed SSL certificates.

After setting up the provider, you can then bootstrap the authentication configuration like this:

[source, terraform]
----
module "oidc" {
  source = "git::https://github.com/camptocamp/devops-stack-module-keycloak.git//oidc_bootstrap?ref=<RELEASE>"

  cluster_name = local.cluster_name
  base_domain  = format("%s.nip.io", replace(module.ingress.external_ip, ".", "-"))

  dependency_ids = {
    keycloak = module.keycloak.id
  }
}
----

=== User Configuration

By default, the `oicd_bootstrap` module creates a basic realm containing a placeholder user that you can use out-of-the-box to authenticate to the other applications on the DevOps Stack.

However, you can provide a map of desired users and the submodule creates them all with an initial password that can then be changed.

Simply declare the module as follows:

[source, terraform]
----
module "oidc" {
  source = "git::https://github.com/camptocamp/devops-stack-module-keycloak.git//oidc_bootstrap?ref=<RELEASE>"

  cluster_name = local.cluster_name
  base_domain  = format("%s.nip.io", replace(module.ingress.external_ip, ".", "-"))

  user_map = {
    johndoe = {
      username   = "johndoe"
      first_name = "John"
      last_name  = "Doe"
      email      = "john.doe@example.com"
    }
    janedoe = {
      username   = "janedoe"
      first_name = "Jane"
      last_name  = "Doe"
      email      = "jane.doe@example.com"
    }
  }

  dependency_ids = {
    keycloak = module.keycloak.id
  }
}
----

NOTE: All the fields on each user are required. Besides, since the e-mail is a scope required by most of our apps, the e-mail is automatically set as verified when the users are created.

IMPORTANT: All users will belong to the administrators group and will have high privileges in applications such as Argo CD.

The module contains an output called `devops_stack_users_passwords` where you can get a map containing every username and their respective initial password.

=== OIDC Configuration

By default, the OIDC client is configured to allow returning to any URL after the authentication is successful. If you prefer, you can restrict only the redirect URIs to a list of domains using the input variable `oidc_redirect_uris`:

[source, terraform]
----
module "oidc" {
  source = "git::https://github.com/camptocamp/devops-stack-module-keycloak.git//oidc_bootstrap?ref=<RELEASE>"

  cluster_name = local.cluster_name
  base_domain  = format("%s.nip.io", replace(module.ingress.external_ip, ".", "-"))

  oidc_redirec_uris = [
    "https://argocd.apps.${local.cluster_name}.${format("%s.nip.io", replace(module.ingress.external_ip, ".", "-"))}/auth/callback",
    "https://grafana.apps.${local.cluster_name}.${format("%s.nip.io", replace(module.ingress.external_ip, ".", "-"))}/login/generic_oauth",
    "https://prometheus.apps.${local.cluster_name}.${format("%s.nip.io", replace(module.ingress.external_ip, ".", "-"))}/oauth2/callback",
    "https://thanos-query.apps.${local.cluster_name}.${format("%s.nip.io", replace(module.ingress.external_ip, ".", "-"))}/oauth2/callback",
    "https://thanos-bucketweb.apps.${local.cluster_name}.${format("%s.nip.io", replace(module.ingress.external_ip, ".", "-"))}/oauth2/callback",
    "https://alertmanager.apps.${local.cluster_name}.${format("%s.nip.io", replace(module.ingress.external_ip, ".", "-"))}/oauth2/callback",
  ]

  dependency_ids = {
    keycloak = module.keycloak.id
  }
}
----

The module provides and output called `oidc` containing the OIDC configuration that is to be passed on to other modules. This output is an object that outputs the content of `local.oidc`:

[source, terraform]
----
locals {
  oidc = {
    issuer_url    = format("https://keycloak.apps.%s.%s/realms/devops-stack", var.cluster_name, var.base_domain)
    oauth_url     = format("https://keycloak.apps.%s.%s/realms/devops-stack/protocol/openid-connect/auth", var.cluster_name, var.base_domain)
    token_url     = format("https://keycloak.apps.%s.%s/realms/devops-stack/protocol/openid-connect/token", var.cluster_name, var.base_domain)
    api_url       = format("https://keycloak.apps.%s.%s/realms/devops-stack/protocol/openid-connect/userinfo", var.cluster_name, var.base_domain)
    client_id     = "devops-stack-applications"
    client_secret = resource.random_password.client_secret.result
    oauth2_proxy_extra_args = var.cluster_issuer == "ca-issuer" ? [
      "--insecure-oidc-skip-issuer-verification=true",
      "--ssl-insecure-skip-verify=true",
    ] : []
  }
}
----

== Technical Reference

=== Dependencies

==== `module.keycloak`

Obviously, this module requires that `module.keycloak` is deployed before, because in needs a working Keycloak instance in which to create its resources. 

// BEGIN_TF_DOCS
// END_TF_DOCS

=== Reference in table format 

.Show tables
[%collapsible]
====
// BEGIN_TF_TABLES
// END_TF_TABLES
====
